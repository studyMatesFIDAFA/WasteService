System sprint1

// MESSAGGI
Request load_req : load_req(TIPO, PESO)
Reply loadaccepted: loadaccepted(TIPO, PESO)
Reply loadrejected: loadrejected(TIPO, PESO)

Dispatch start_trasf:start_trasf(X,Y,TIPO, PESO)

Request other_load: other_load(ARG)
Reply yes : yes(X,Y,TIPO, PESO)
Reply no : no(ARG)

Event load_pickup: load_pickup(TIPO, PESO)
Event waste_state:waste_state(PESOP, PESOG)
Event trolley_state : trolley_state(STATO)

// CONTESTO MONOLITICO
Context ctxWasteService ip [host="localhost" port=8050]


//ATTORI 

// TROLLEY
QActor trolley context ctxWasteService {
	
	// se il trolley è a conoscenza delle coordinate uso questi valori
	// altrimenti aggiungerò le coordinate come elementi passati nella richiesta
	[#
		val IndoorX = 0
		val IndoorY = 10
		val HomeX = 0
		val HomeY = 0
		val GlassX = 10
		val GlassY = 0
		val PlasticX = 10
		val PlasticY = 10
		
		var Stato="home"
		var X=0
		var Y=0
		var Tipo_carico = ""
		var Peso_carico = 0
		val DelayHome=2000L
		val DelayBox=3000L
	#]
	
	State start initial{
		printCurrentMessage
		println("TROLLEY | START")
	}
	Goto home
	
	State home{
		printCurrentMessage
		println("TROLLEY | HOME")
		println("TROLLEY | Attendo un compito dal Waste Service")
		[#Stato = "home"#]
		emit trolley_state : trolley_state($Stato)
	}
	Transition t0 whenMsg start_trasf -> trasferimento_indoor
	
	State trasferimento_indoor{
		printCurrentMessage
		println("TROLLEY | TRASFERIMENTO INDOOR")
		[# Stato="in_movimento" #]
		emit trolley_state : trolley_state($Stato)
		onMsg(start_trasf:start_trasf(X,Y,TIPO, PESO)){
			[#
				X=payloadArg(0).toInt()
				Y=payloadArg(1).toInt()
				Tipo_carico = payloadArg(2)
				Peso_carico = payloadArg(3).toInt()
			#]
		}
		onMsg(yes : yes(X,Y,TIPO, PESO)){
			[#
				X=payloadArg(0).toInt()
				Y=payloadArg(1).toInt()
				Tipo_carico = payloadArg(2)
				Peso_carico = payloadArg(3).toInt()
			#]
		}
		//println("TROLLEY | Coordinate ($X, $Y)")
		delayVar DelayHome
	}
	Goto carico_rifiuti
	
	State carico_rifiuti{
		printCurrentMessage
		println("TROLLEY | CARICO RIFIUTI")
		[# Stato="fermo" #]
		emit trolley_state : trolley_state($Stato)
		emit load_pickup: load_pickup($Tipo_carico, $Peso_carico)
	}
	Goto trasferimento_carico
	
	State trasferimento_carico{
		printCurrentMessage
		println("TROLLEY | TRASFERIMENTO CARICO")
		[# Stato="in_movimento" #]
		emit trolley_state : trolley_state($Stato)
		if [# Tipo_carico.equals("P") #]{
			println("TROLLEY | Direzione PLASTIC BOX")
		}
		else {
			println("TROLLEY | Direzione GLASS BOX")
		}
		delayVar DelayBox
		[# Stato="fermo" #]
		emit trolley_state : trolley_state($Stato)
		println("TROLLEY | FINE TRASFERIMENTO $Tipo_carico $Peso_carico")
		request waste_service -m other_load : other_load(x)
	}
	Transition t1 whenReply yes -> trasferimento_indoor
				whenReply no -> ritorno_home
	
				
	State ritorno_home{
		printCurrentMessage
		println("TROLLEY | RITORNO HOME")
		[# Stato="in_movimento" #]
		emit trolley_state : trolley_state($Stato)
		delayVar DelayHome
	}
	Goto home
}


// WASTE SERVICE
QActor waste_service context ctxWasteService{
	//I VALORI 200 sarebbero MAXPB e MAXGB
	[#
		var MAXPB = 10
		var MAXPG = 10
		var Kg_plastica=MAXPB
		var Kg_vetro=MAXPG
		var Trolley_occupato=false	
		var Carico_accettato = false
		var Tipo_carico = ""
		var Peso_carico = 0
	#]
	
	State start initial{
		printCurrentMessage
		println("WASTE SERVICE | START")
	}
	Goto attesa_camion
	
	State attesa_camion{
		printCurrentMessage
		println("WASTE SERVICE | ATTESA CAMION")
		
		onMsg (other_load : other_load(ARG)){
			[# Trolley_occupato = false #]
			replyTo other_load with no : no(ARG)
		}
	}
	Transition t0 whenRequest load_req -> gestisci_richiesta
					whenRequest other_load -> attesa_camion
	
	State gestisci_richiesta{
		printCurrentMessage
		println("WASTE SERVICE | GESTIONE RICHIESTA")
		onMsg(load_req : load_req(TIPO, PESO)){
			[#
				Tipo_carico = payloadArg(0)
				Peso_carico = payloadArg(1).toInt()
			#]
			if [# Tipo_carico.equals("P") #]{
				if [#Kg_plastica-Peso_carico>=0#]{
					//loadaccepted
					replyTo load_req with loadaccepted : loadaccepted($Tipo_carico, $Peso_carico)
					[#
						Kg_plastica = Kg_plastica - Peso_carico
						Carico_accettato=true
					#]
				}
				else{
					//loadrejected
					replyTo load_req with loadrejected : loadrejected($Tipo_carico, $Peso_carico)
					[#
						Carico_accettato=false
					#]
				}
			}
			else {
				if [# Tipo_carico.equals("G") #] {
					if [#Kg_vetro-Peso_carico>=0#]{
						replyTo load_req with loadaccepted : loadaccepted($Tipo_carico, $Peso_carico)
					[#
						Kg_vetro = Kg_vetro - Peso_carico
						Carico_accettato=true
					#]
					}
					else{
						replyTo load_req with loadrejected : loadrejected($Tipo_carico, $Peso_carico)
						[#
							Carico_accettato=false
						#]
					}
				}
				else{
					println("WASTE SERVICE | ERRORE: TIPO CARICO SBAGLIATO")
				}
			}
		}
		
	}
	Goto gestione_trolley if [# Carico_accettato #] else attesa_camion 
	
	
	State gestione_trolley{
		printCurrentMessage
		println("WASTE SERVICE | GESTIONE TROLLEY")
		//battezzo (6,0) come coordinate del glass box e (6,6) come coordinate del plastic box
		if [# Trolley_occupato.equals(false) #]{
			println("WASTE SERVICE | Gestico trolley LIBERO")
			if [# Tipo_carico.equals("G") #]{
				//vetro
				forward trolley -m start_trasf : start_trasf(6,0,$Tipo_carico, $Peso_carico)
			}
			else{
				//plastica
				forward trolley -m start_trasf : start_trasf(6,6,$Tipo_carico, $Peso_carico)
			}
			
			
		}
	}
	Goto trolley_occupato if [# Trolley_occupato #] else attesa_trolley
	
	State trolley_occupato{
		printCurrentMessage
		println("WASTE SERVICE | TROLLEY OCCUPATO")
	}
	Transition t2 whenRequest other_load -> attesa_trolley
	
	State attesa_trolley{
		printCurrentMessage
		println("WASTE SERVICE | ATTESA TROLLEY")
		onMsg (other_load: other_load(ARG)){
			if [# Tipo_carico.equals("P") #]{
				replyTo other_load with yes : yes(6,6,$Tipo_carico, $Peso_carico)
			}
			else{
				replyTo other_load with yes : yes(6,0,$Tipo_carico, $Peso_carico)
			}
		}
		//è giusto mettere qua trolley occupato?
		[#
			Trolley_occupato = true
			var Kg_occ_p = MAXPB-Kg_plastica
			var Kg_occ_g = MAXPG-Kg_vetro
		#]
		emit waste_state:waste_state($Kg_occ_p, $Kg_occ_g)
	}
	Transition t1 whenEvent load_pickup -> attesa_camion
	
}

/*
QActor waste_truck context ctxWasteService{
	State start initial{
		println("CAMION DEL RUSCO")
		delay 1000
	}
	Goto richiesta_scarico
	
	State richiesta_scarico{
		println("CAMION: richiedo scarico di 50kg di vetro")
		request waste_service -m load_req : load_req(G, 50)
	}
	Transition t0 whenReply loadaccepted -> carico_accettato
					whenReply loadrejected -> carico_rifiutato
					
	State carico_accettato{
		println("CAMION, carico accettato, attendo trolley...")
	}
	Transition t1 whenEvent load_pickup -> carico_prelevato
	
	State carico_rifiutato{
		println("CAMION, carico rifiutato, lascio sistema")
	} 
	
	State carico_prelevato{
		println("CAMION, carico prelevato, ciao ciao")
	}Goto richiesta_scarico_2
	
	State richiesta_scarico_2{
		println("CAMION: richiedo scarico di 50kg di plastica")
		request waste_service -m load_req : load_req(P, 50)
	}
	Transition t1 whenReply loadaccepted -> carico_accettato
					whenReply loadrejected -> carico_rifiutato
}

 */
 
 // MOCK
 
 // WASTE TRUCK
 // test plan
 QActor wastetruckmock context ctxWasteService {
	State start initial {
		printCurrentMessage
		println("WASTE TRUCK | START")
	} Goto camion1_req
	
	State camion1_req {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 1 REQ (GLASS, 8)")
		request waste_service -m load_req : load_req(G, 8)
	} Transition t7 whenReply loadaccepted -> camion1_accepted
					whenReply loadrejected -> not_as_planned
					
	State camion1_accepted {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 1 ACCETTATO")
	} Transition t0 whenEvent load_pickup -> camion1_terminazione
	
	State camion1_terminazione {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 1 TERMINA")
	} Goto camion2_req
	
	State camion2_req {
		printCurrentMessage
		
		println("WASTE TRUCK | CAMION 2 REQ (GLASS, 3)")
		request waste_service -m load_req : load_req(G, 3)
	} Transition t1 whenReply loadrejected -> camion2_rejected
					whenReply loadaccepted -> not_as_planned
					
	State camion2_rejected {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 2 REJECTED")
	} Goto camion3_req
	
	State camion3_req {
		printCurrentMessage
		delay 250
		println("WASTE TRUCK | CAMION 3 REQ (PLASTIC, 4)")
		request waste_service -m load_req : load_req(P, 4)
	} Transition t2 whenReply loadaccepted -> camion4_req
					whenReply loadrejected -> not_as_planned
					
	State camion4_req {
		printCurrentMessage
		delay 250
		println("WASTE TRUCK | CAMION 4 REQ (PLASTIC, 3)")
		request waste_service -m load_req : load_req(P, 3)
	} Transition t3 whenReply loadaccepted -> camion5_req
					whenReply loadrejected -> not_as_planned
				
	State camion5_req {
		printCurrentMessage
		delay 250
		println("WASTE TRUCK | CAMION 5 REQ (PLASTIC, 8)")
		request waste_service -m load_req : load_req(P, 8)
	} Transition t4 whenReply loadrejected -> camion3_accepted
					whenReply loadaccepted -> not_as_planned
	
	State camion3_accepted {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 3 ACCETTATO")
	} Transition t5 whenEvent load_pickup -> camion3_terminazione
	
	State camion3_terminazione {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 3 TERMINA")
	} Goto camion4_accepted
	
	State camion4_accepted {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 4 ACCETTATO")
	} Transition t6 whenEvent load_pickup -> camion4_terminazione
	
	State camion4_terminazione {
		printCurrentMessage
		println("WASTE TRUCK | CAMION 4 TERMINA")
	} Goto fine
	
	State fine {
		printCurrentMessage
		println("WASTE TRUCK | FINE!")
	}				
	
	
	State not_as_planned {
		printCurrentMessage 
		println("WASTE TRUCK | ERRORE!!!!!")
	} 
}


// GUI 
QActor guimock context ctxWasteService {
	
	[#
		var CaricoP = 0
		var CaricoG = 0
		var StatoTrolley = "home"
	#]
	
	
	State start initial {
		printCurrentMessage
		println("GUI | START")
		println("GUI | STATO TROLLEY: $StatoTrolley")
		println("GUI | CARICO PLASTIC: $CaricoP")
		println("GUI | CARICO GLASS: $CaricoG")
	} Transition t0 whenEvent trolley_state -> aggiornamento_trolley
					whenEvent waste_state -> aggiornamento_carico
	
	State aggiornamento_trolley {
		printCurrentMessage
		println("GUI | AGGIORNAMENTO TROLLEY")
		
		onMsg (trolley_state : trolley_state ( STATE )) {
			
		[#
			StatoTrolley = payloadArg(0)
		#]
		}
			
		println("GUI | STATO TROLLEY: $StatoTrolley")
		println("GUI | CARICO PLASTIC: $CaricoP")
		println("GUI | CARICO GLASS: $CaricoG")
	} Transition t0 whenEvent trolley_state -> aggiornamento_trolley
					whenEvent waste_state -> aggiornamento_carico
						
	State aggiornamento_carico {
		printCurrentMessage
		println("GUI | AGGIORNAMENTO TROLLEY")
		
		onMsg (waste_state:waste_state(PESOP, PESOG)) {
			
		[#
			CaricoP = payloadArg(0).toInt()
			CaricoG = payloadArg(1).toInt()
		#]
		}
			
		println("GUI | STATO TROLLEY: $StatoTrolley")
		println("GUI | CARICO PLASTIC: $CaricoP")
		println("GUI | CARICO GLASS: $CaricoG")
	} Transition t0 whenEvent trolley_state -> aggiornamento_trolley
						whenEvent waste_state -> aggiornamento_carico
}

